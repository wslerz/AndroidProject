if (isCenterModule.toBoolean()) {
    //组件开发模式
    apply plugin: 'com.android.application'
} else {
    //集成开发模式
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

//ARouter 集成开发模式下才提供服务
if (isCenterModule.toBoolean()) {
    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi-v7a" //, "armeabi","arm64-v8a", "x86","x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (isCenterModule.toBoolean()) {
                //组件开发模式   需要设置启动activity
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    //用来放组件开发模式的类  注意不能让com包下的类调用
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    repositories  {
        flatDir{ dirs 'libs'  }
    }


    //设置资源的前缀  防止布局 style等重复
    resourcePrefix "center_"

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':common_lib')
    //ARouter 集成开发模式下才提供服务
    if (isCenterModule.toBoolean()) {
        kapt "com.alibaba:arouter-compiler:${rootProject.ext.dependVersion.arouterCompilerVersion}"
    }
}
